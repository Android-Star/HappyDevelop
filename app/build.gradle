apply plugin: 'com.android.application'

def getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

ext {
    apkPrefix = 'SmartPhone'
    //keystore = 'key/platform.jks'
    keystore = 'key/xiezhu.keystore'
    appVersionName = '1.0.1'
    appVersionCode = '1'
}

def loadProperties() {
    def proFile = file("../build.properties")
    Properties pro = new Properties()
    proFile.withInputStream { stream -> pro.load(stream) }
    appVersionName = pro.version
    appVersionCode = Integer.parseInt(pro.versioncode)
}

loadProperties()

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.example.wilsonhan.happydevelop"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions { annotationProcessorOptions { includeCompileClasspath = true } }
    }
    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile = file("${keystore}")
            storePassword = STORE_PASSWORD
            keyAlias = KEY_ALIAS
            keyPassword = KEY_PASSWORD
        }

        release {
            storeFile = file("${keystore}")
            storePassword = STORE_PASSWORD
            keyAlias = KEY_ALIAS
            keyPassword = KEY_PASSWORD
        }
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${apkPrefix}-" + appVersionName +
                "-" +
                appVersionCode +
                "-" +
                "${rootProject.ext.apkBuildTime}-${getGitVersion()}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }
    sourceSets {
        main {
            jniLibs.srcDir 'libs'
        }
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

repositories {
    mavenCentral()
    maven {
        url new File(project.projectDir, 'libs')
    }
    flatDir {
        dirs project(':common').file('libs')
    }
}

dependencies {
    implementation project(':common')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:recyclerview-v7:27.0.0'
    implementation 'com.android.support:cardview-v7:27.0.0'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation "com.jakewharton:butterknife:8.5.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:8.5.1"
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-21'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-21'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46'
    implementation 'com.github.bumptech.glide:glide:4.5.0'
    implementation 'com.gyf.immersionbar:immersionbar:2.3.3-beta15'
    implementation 'com.github.lygttpod:SuperTextView:2.4.2'
    implementation 'com.github.chenBingX:SuperTextView:3.2.5.99'
    implementation 'com.haibin:calendarview:3.6.6'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'
}